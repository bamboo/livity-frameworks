configurations {
  lib
}

dependencies {
  lib 'nuget:NUnit:2.6.3'
  lib 'nuget:NUnit.Runners:2.6.3'
}

repositories {
  ivy {
    url 'https://raw.githubusercontent.com/bamboo/artifacts/master'
  }
}

project(':src') {
  subprojects { component ->

    apply plugin: 'base'

    group = 'github.bamboo'

    version = '1.0'

    task zip(type: Zip) {
      dependsOn ':build'
      from 'bin/Release'
      include "${component.name}.dll"
      include "${component.name}.xml"
    }

    artifacts {
      'default' zip
    }

    uploadArchives {
      repositories {
        ivy {
          url rootProject.file('../artifacts')
        }
      }
    }

    task publish {
      dependsOn uploadArchives
    }
  }
}

project(':src:Livity.Composition') {
  dependencies {
    'default' project(':src:Livity.Collections')
  }
}

task install {
  inputs.source configurations.lib
  outputs.dir 'lib'
  doFirst {
    inputs.files.each { artifact ->
      def artifactId = artifact.name.split('-')[0]
      copy {
        from zipTree(artifact)
        into project.file("lib/$artifactId")
      }
    }
  }
}

class OS {
  static def isWindows() {
    return System.properties['os.name'].toLowerCase().contains('windows')
  }
}

class XBuild extends Exec {
  XBuild() {
    executable(OS.isWindows() ? 'msbuild' : 'xbuild')
  }

  def target(t) {
    args "/target:$t"
  }

  def configuration(c) {
    args "/p:Configuration=$c"
  }
}

task build(type: XBuild) {
  dependsOn install
  target 'Rebuild'
  configuration 'Release'
}

task clean(type: XBuild) {
  target 'Clean'
}

task debug(type: XBuild) {
  dependsOn install
  target 'Build'
  configuration 'Debug'
}

task check(type: Exec) {
  dependsOn debug
  inputs.source {
    fileTree(dir: 'src', include: '**/bin/Debug/*.Tests.dll')
  }
  outputs.file 'TestResult.xml'
  doFirst {
    def nunitRunner= 'lib/NUnit.Runners/tools/nunit-console.exe'
    if (!OS.isWindows()) {
      executable 'mono'
      args nunitRunner
    } else {
      executable nunitRunner
    }
    args inputs.files
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.1'
}

defaultTasks 'check'
